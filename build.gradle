plugins {
    alias(libs.plugins.kotlin) apply false
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs = [
                    '-opt-in=kotlin.contracts.ExperimentalContracts',
                    '-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
            ]
        }
    }
}

ext.applyMatrixServiceModule = { project ->
    def dependencies = project.dependencies
    dependencies.api project.project(":matrix:matrix")
    dependencies.api project.project(":matrix:common")
    dependencies.implementation project.project(":matrix:matrix-http")
    dependencies.implementation libs.kotlin.serialization
}

ext.kotlinTest = { dependencies ->
    dependencies.testImplementation libs.kluent
    dependencies.testImplementation libs.kotlin.test
    dependencies.testImplementation libs.mockk
    dependencies.testImplementation libs.kotlin.coroutines.test

    dependencies.testImplementation libs.junit.jupiter.api
    dependencies.testRuntimeOnly libs.junit.jupiter.engine
}

ext.kotlinFixtures = { dependencies ->
    dependencies.testFixturesImplementation libs.mockk
    dependencies.testFixturesImplementation libs.kluent
    dependencies.testFixturesImplementation libs.kotlin.coroutines
}

apply from: 'tools/coverage.gradle'
